plugins {
	id 'java'
	id 'com.diffplug.spotless' version '6.25.0'
	id 'checkstyle'
	id 'org.springframework.boot' version '3.3.5' apply false
	id 'io.spring.dependency-management' version '1.1.6' apply false
}

group = 'com.kosmin'
version = '0.0.1-SNAPSHOT'

allprojects {
	repositories {
		mavenCentral()
	}
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'com.diffplug.spotless'
	apply plugin: 'checkstyle'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'

	java {
		toolchain {
			languageVersion = JavaLanguageVersion.of(17)
		}
	}

	spotless {
		java {
			googleJavaFormat()
		}
	}

	checkstyle {
		toolVersion = '10.12.0' // Set Checkstyle version
		configFile = file("$rootDir/api-gateway/src/main/resources/checkstyle.xml")
	}

	test {
		useJUnitPlatform()
	}

	tasks.register("unitTest", Test) {
		useJUnitPlatform {
			excludeTags "int"
		}
	}

	dependencies {
		// Spring dependencies
		implementation 'org.springframework.boot:spring-boot-starter'
		implementation 'org.springframework.boot:spring-boot-starter-web'

		// Lombok
		implementation 'org.projectlombok:lombok:1.18.34'
		annotationProcessor 'org.projectlombok:lombok:1.18.34'

		// Jakarta Validator
		implementation 'jakarta.validation:jakarta.validation-api:3.1.0'
		implementation 'org.hibernate.validator:hibernate-validator:8.0.1.Final'
		implementation 'jakarta.el:jakarta.el-api:6.0.1'

		// test dependencies
		testImplementation 'org.springframework.boot:spring-boot-starter-test'
	}
}
